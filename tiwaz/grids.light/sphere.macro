Macro SPHERE
  // center
  p1 = newp; Point(p1) = {0,0,0,lc};

  // the six points on the coordinate axes.
  p2 = newp; Point(p2) = { r,  0,  0, lc};
  p3 = newp; Point(p3) = { 0,  r,  0, lc};
  p4 = newp; Point(p4) = {-r,  0,  0, lc};
  p5 = newp; Point(p5) = { 0, -r,  0, lc};
  p6 = newp; Point(p6) = { 0,  0, -r, lc};
  p7 = newp; Point(p7) = { 0,  0,  r, lc};

  // arcs
  l01 = newl; Circle(l01) = {p2, p1, p3};
  l02 = newl; Circle(l02) = {p3, p1, p4};
  l03 = newl; Circle(l03) = {p4, p1, p5};
  l04 = newl; Circle(l04) = {p5, p1, p2};
  l05 = newl; Circle(l05) = {p3, p1, p6};
  l06 = newl; Circle(l06) = {p6, p1, p5};
  l07 = newl; Circle(l07) = {p5, p1, p7};
  l08 = newl; Circle(l08) = {p7, p1, p3};
  l09 = newl; Circle(l09) = {p2, p1, p7};
  l10 = newl; Circle(l10) = {p7, p1, p4};
  l11 = newl; Circle(l11) = {p4, p1, p6};
  l12 = newl; Circle(l12) = {p6, p1, p2};


  // Nothern hemisphere
  ll1 = newll; Line Loop(ll1) = {  l02,   l08,  -l10};
  ll2 = newll; Line Loop(ll2) = {  l10,   l03,   l07};
  ll3 = newll; Line Loop(ll3) = { -l08,  -l09,   l01};
  ll4 = newll; Line Loop(ll4) = { -l11,  -l02,   l05};

  s1 = news; Surface(s1) = {ll1} In Sphere {p1};
  s2 = news; Surface(s2) = {ll2} In Sphere {p1};
  s3 = news; Surface(s3) = {ll3} In Sphere {p1};
  s4 = news; Surface(s4) = {ll4} In Sphere {p1};

  // Southern hemisphere
  ll5 = newll; Line Loop(ll5) = {  -l05, -l12,  -l01};
  ll6 = newll; Line Loop(ll6) = {  -l03,  l11,   l06};
  ll7 = newll; Line Loop(ll7) = {  -l07,  l04,   l09};
  ll8 = newll; Line Loop(ll8) = {  -l04,  l12,  -l06};

  s5 = news; Surface(s5) = {ll5} In Sphere {p1};
  s6 = news; Surface(s6) = {ll6} In Sphere {p1};
  s7 = news; Surface(s7) = {ll7} In Sphere {p1};
  s8 = news; Surface(s8) = {ll8} In Sphere {p1};

  // Full ball
  sl = newsl; Surface Loop(sl) = {s8, s7, s2, s1, s4, s6, s5, s3};
Return
